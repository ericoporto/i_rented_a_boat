// room script file
Vec3* radiotower_pos;

function room_Load()
{
  cRadioTower.ChangeRoom(player.Room);
  cJulie.ChangeRoom(player.Room, 318, 178);
  cJulie.Baseline = 1;
  cJulie.Transparency = 100;
  cRadioTower.Animate(0, 5, eRepeat, eNoBlock, eForwards);
  cRadioTower.Baseline = 4;
  
  Sound_AmbientPlay(aAmb_island);
  radiotower_pos = Vec3.Create(754.0, 586.0,  64.0); 
  
  player.x = 16;
  player.y = 16;
  CustomTransition_IntantFadeOut();
  
  if(bool_balloon_in_position) {
    hotspot[4].SetTextProperty("aName","Talk to Julie");
  }
}

function room_AfterFadeIn()
{
  CustomTransition_FadeIn(0.33, eBlockTween);
  if(Game.DoOnceOnly("save_first_time_in_deadend"))
  {
    Wait(1);
    CustomSave.doAutoSave();
  }
}

function repeatedly_execute_always()
{ 
  if(Frame & 4) return;
 
  obj_sky.X = -ScreenWidth*2 + FloatToInt(Player_Angle*IntToFloat(ScreenWidth)/Maths.Pi);
  
  ScreenPoint* billproj = CharacterProjectionAdjust(cRadioTower,  64.0, radiotower_pos);
  int bill_dist = FloatToInt(AbsF(billproj.z));
  cRadioTower.Scaling = ClampI(26000/(256+bill_dist), 10, 100); // less true but better in game scaling
  cRadioTower.Tint(54, 43, 57, (100 - cRadioTower.Scaling)/2, 100 - cRadioTower.Scaling/2);
  
  RenderCurrentMapSurfaceDetail(48.0, 350.0, 32.0);  
}


function room_RepExec()
{
}

function hHotspot20_Interact()
{
  CustomTransition_FadeOut(0.25, eBlockTween);
  player.ChangeRoom(2, 434, 108);
}

function hHotspot4_Talk()
{
  if(Game.DoOnceOnly("talk_julie_2nd"))
  {
    dFirstTalk.SetOptionState(1, eOptionOff);
    dFirstTalk.SetOptionState(2, eOptionOff);
    dFirstTalk.SetOptionState(3, eOptionOff);
    dFirstTalk.SetOptionState(4, eOptionOff);
    dFirstTalk.SetOptionState(5, eOptionOff);
    dFirstTalk.SetOptionState(6, eOptionOff);
    dFirstTalk.SetOptionState(7, eOptionOff);
    dFirstTalk.SetOptionState(8, eOptionOff);
    dFirstTalk.SetOptionState(9, eOptionOff);
    dFirstTalk.SetOptionState(10, eOptionOn);
  }
  
  if(Game.DoOnceOnly("found_place_you_fell"))
  {
    Objective.Complete(eObjective_FindThePlaceYouFell);  
  }
  
  if(bool_balloon_in_position)
  {
    // Julie, you there?
    StartDialog(dDeadEndFinal);    
  }
  else 
  {
    StartDialog(dDeadEndDialog);
  }
}
